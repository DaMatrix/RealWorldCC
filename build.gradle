/*
 * Adapted from the Wizardry License
 *
 * Copyright (c) 2018-2020 DaPorkchop_ and contributors
 *
 * Permission is hereby granted to any persons and/or organizations using this software to copy, modify, merge, publish, and distribute it. Said persons and/or organizations are not allowed to use the software or any derivatives of the work for commercial use or any other means to generate income, nor are they allowed to claim this software as their own.
 *
 * The persons and/or organizations are also disallowed from sub-licensing and/or trademarking this software without explicit permission from DaPorkchop_.
 *
 * Any persons and/or organizations using this software must disclose their source code and have it publicly available, include this license, provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NON INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

buildscript {
    repositories {
        jcenter()
        maven {
            name = "Minecraft Forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.0"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"

ext {
    //general things
    javaVersion = "1.8"
    flatearthVersion = "0.0.1." + ('git rev-list --count HEAD'.execute().text.trim())

    //minecraft things
    mcpmappingsVersion = "stable_39"
    minecraftforgeVersion = "14.23.5.2847"
    minecraftVersion = "1.12.2"

    //dependency things
    junitVersion = "4.12"
    leveldbVersion = "0.0.10-SNAPSHOT"
    lombokVersion = "1.16.20"
    porklibVersion = "0.5.5-SNAPSHOT"
}

version = "$flatearthVersion"
group = "net.daporkchop"

compileJava {
    sourceCompatibility = targetCompatibility = "$javaVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    mavenCentral()
    maven {
        name = "Sonatype"
        url = "https://oss.sonatype.org/content/repositories/public/"
    }
    maven {
        name = "SpongePowered"
        url = "http://repo.spongepowered.org/maven/"
    }
    maven {
        name = "Apache Development Snapshot Repository"
        url = "https://repository.apache.org/content/repositories/snapshots/"
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    deobfCompile "io.github.opencubicchunks:cubicchunks-api:1.12.2-0.0-SNAPSHOT"

    deobfCompile "io.github.opencubicchunks:cubicchunks:1.12.2-0.0-SNAPSHOT"
    deobfCompile "io.github.opencubicchunks:cubicworldgen:1.12.2-0.0.40.0-SNAPSHOT"

    compile "org.apache.commons:commons-imaging:1.0-SNAPSHOT"

    compile "net.daporkchop.lib:binary:0.3.0-SNAPSHOT"
    compile "net.daporkchop.lib:concurrent:0.3.0-SNAPSHOT"
    compile "net.daporkchop.lib:graphics:0.3.0-SNAPSHOT"
    compile "net.daporkchop.lib:logging:0.3.0-SNAPSHOT"

    compile "net.daporkchop.lib:hash:0.3.0-SNAPSHOT"
    compile "net.daporkchop.lib:http:0.3.0-SNAPSHOT"

    /*compile "org.apache.commons:commons-math3:3.6.1"
    compile "com.mortennobel:java-image-scaling:0.8.6"
    compile "com.github.haifengl:smile-interpolation:1.5.1"

    compile "net.daporkchop.lib:db:0.1.4"
    compile "net.daporkchop.lib:primitive:0.0.6"*/

    testCompile "junit:junit:$junitVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

minecraft {
    version = "$minecraftVersion-$minecraftforgeVersion"
    runDir = "run"
    mappings = "$mcpmappingsVersion"

    makeObfSourceJar = false
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    classifier = null
    configurations = [ project.configurations.shade ]
}
build.dependsOn shadowJar

reobf {
    shadowJar {}
}

task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.daporkchop.flatearth.dep"
}
shadowJar.dependsOn relocateShadowJar
